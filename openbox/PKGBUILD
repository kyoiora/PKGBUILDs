# Maintainer:  Matthew Monaco <net 0x01b dgbaley27>

__gitroot="git://github.com/danakj/openbox.git"
__gitname="openbox.git"
__gitcommit="master"

pkgname=openbox
pkgver=3.5.0+git
pkgrel=7
pkgdesc="A window manager for the X11 windowing system"
arch=('i686' 'x86_64')
url="http://openbox.org"
license=('GPL')
depends=('startup-notification' 'libxml2' 'libxinerama' 'libxrandr'
         'libxcursor' 'pango' 'imlib2' 'libsm')
optdepends=('pyxdg: for the xdg-autostart script')
groups=('lxde')
options=('!libtool')
source=(
)
md5sums=(
)
backup=(
	'etc/xdg/openbox/menu.xml'
	'etc/xdg/openbox/rc.xml'
)

build() {

	_git_setup
	cd "$srcdir/$pkgname-$pkgver"

	./bootstrap
	./configure --prefix=/usr \
		--with-x \
		--enable-startup-notification \
		--sysconfdir=/etc \
		--libexecdir=/usr/lib/openbox

	make
}

package() {

	cd "$srcdir/$pkgname-$pkgver"
	make DESTDIR="$pkgdir" install
}

_git_setup() {

	cd "$SRCDEST"

	if [[ ! -d "$__gitname" ]]; then
		msg2 "Cloning git repository"
		git clone --mirror "$__gitroot" "$__gitname"
	else
		msg2 "Updating git repository"
		cd "$__gitname"
		git fetch
	fi

	cd "$srcdir"
	if [[ -d "$pkgname-$pkgver" ]]; then
		msg2 "Removing previous build tree"
		rm -fr "$pkgname-$pkgver"
	fi

	msg2 "Creating fresh build tree"
	git clone --depth=1 -b "$__gitcommit" "file://$SRCDEST/$__gitname" "$pkgname-$pkgver"
}
