# Maintainer:  Jesse Jaara <jaara@gmail.com>
# Contributor: Matt Monaco <dgbaley27@0x01b.net>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Tobias Powalowski  <tpowa@archlinux.org>
# Contributor: Thomas BÃ¤chler <thomas@archlinux.org>
# Contributor: Alexander Baldeck <alexander@archlinux.org>

_realname=xf86-input-synaptics
_suseurl='FILE::https://api.opensuse.org/public/source/openSUSE:Factory/xorg-x11-driver-input/FILE?rev=67f06298aa561ce2152cad26f92cf4ae&'
pkgname=$_realname-clickpad
pkgver=1.5.0
pkgrel=3
pkgdesc="Synaptics driver for notebook touchpads (with clickpad and led support)"
arch=('i686' 'x86_64')
license=('custom')
url="http://xorg.freedesktop.org/"
depends=('libxtst')
makedepends=('xorg-server-devel>=1.10.99.902' 'libxi' 'libx11')
optdepends=('synaptics-led')
conflicts=('xorg-server<1.10.99.902')
replaces=('synaptics')
provides=('synaptics' $_realname)
conflicts=('synaptics' $_realname)
groups=('xorg-drivers' 'xorg')
options=(!libtool)
backup=('etc/X11/xorg.conf.d/10-synaptics.conf')
source=(http://xorg.freedesktop.org/releases/individual/driver/${_realname}-${pkgver}.tar.bz2
        ${_suseurl//FILE/xf86-input-synaptics-wait.diff}
        ${_suseurl//FILE/xf86-input-synaptics-settings.diff}
        ${_suseurl//FILE/xf86-input-synaptics-add-clickpad-support.diff}
        ${_suseurl//FILE/xf86-input-synaptics-clickpad-doc-update.diff}
        ${_suseurl//FILE/xf86-input-synaptics-add-led-support.diff}
        ${_suseurl//FILE/xf86-input-synaptics-led-double-tap.diff}
        ${_suseurl//FILE/xf86-input-synaptics-fix-clickpad-capabilities.diff}
        ${_suseurl//FILE/xf86-input-synaptics-clickpad-threshold.diff}
        ${_suseurl//FILE/xf86-input-synaptics-move-threshold.diff}
        ${_suseurl//FILE/xf86-input-synaptics-filter-bogus-coord.diff}
        10-synaptics.conf)
sha1sums=('7373c1e3f02bf7e18f71b65762a982d907b4a053'
          '3355a0beeb0e655a9f0ab5ff08e76ba62beb76f6'
          '8207af069b4a8b18d46ae4142782f6dfe26c9818'
          '61f4f0cc4a071985447c6f0c7ce11c060e829aa1'
          'a5880fc68b1888177a04b467c92a74d2c13997dc'
          'ff0c6bcb85d32b84273b3e132c2f0fe94503e903'
          'cfce7d9d231358a67d7d9d930fe7f38cdadd39c7'
          'af02cb73fa4f46e63789d64ca711826c7659e445'
          'd1d2609a45eaeb5ae76ca73f7c88e0374f6b83b6'
          'a94d408dd464e61a09d7c5338f03c241ebdbd60e'
          'f0e7b679c9e3027db69d9d3c0af7bd1b896d88a6'
          '68e1f4ef5e1038231d210eb422fa4d18c5922f0f')

build() {
  cd "${srcdir}/${_realname}-${pkgver}"
  for p in "${source[@]}"; do
    p="${p%%::*}"
    [[ "$p" =~ ^.+\.diff$ ]] || continue
    msg2 "Applying patch: $p"
    patch -p1 -i "../$p"
  done
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/etc/X11/xorg.conf.d"
  install -m644 "${srcdir}/10-synaptics.conf" "${pkgdir}/etc/X11/xorg.conf.d/"
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"

  rm -rf "${pkgdir}/usr/share/X11"
}
