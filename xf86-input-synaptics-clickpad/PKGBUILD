# Maintainer:  Jesse Jaara <jaara@gmail.com>
# Contributor: Matt Monaco <dgbaley27@0x01b.net>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Tobias Powalowski  <tpowa@archlinux.org>
# Contributor: Thomas BÃ¤chler <thomas@archlinux.org>
# Contributor: Alexander Baldeck <alexander@archlinux.org>

_realname=xf86-input-synaptics
pkgname=$_realname-clickpad
pkgver=1.5.0
pkgrel=2
pkgdesc="Synaptics driver for notebook touchpads (with clickpad and led support)"
arch=('i686' 'x86_64')
license=('custom')
url="http://xorg.freedesktop.org/"
depends=('libxtst')
makedepends=('xorg-server-devel>=1.10.99.902' 'libxi' 'libx11')
optdepends=('synaptics-led')
conflicts=('xorg-server<1.10.99.902')
replaces=('synaptics')
provides=('synaptics' $_realname)
conflicts=('synaptics' $_realname)
groups=('xorg-drivers' 'xorg')
options=(!libtool)
backup=('etc/X11/xorg.conf.d/10-synaptics.conf')
source=(http://xorg.freedesktop.org/releases/individual/driver/${_realname}-${pkgver}.tar.bz2
        synaptics-clickpad-support.patch
        10-synaptics.conf)
sha1sums=('7373c1e3f02bf7e18f71b65762a982d907b4a053'
          '23dd5ca4838acc0a4cd04dbba8ba19c9c27e84ca'
          '68e1f4ef5e1038231d210eb422fa4d18c5922f0f')

build() {
  cd "${srcdir}/${_realname}-${pkgver}"
  patch -p1 -i ../synaptics-clickpad-support.patch
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/etc/X11/xorg.conf.d"
  install -m644 "${srcdir}/10-synaptics.conf" "${pkgdir}/etc/X11/xorg.conf.d/"
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"

  rm -rf "${pkgdir}/usr/share/X11"
}
