# Maintainer: Matthew Monaco <dgbaley27@0x01b.net>

_kver=3.2
_gitroot=git://repo.or.cz/linux-2.6.git

pkgname=synaptics-led
pkgver=$_kver
pkgrel=1

arch=(i686 x86_64)
license=(GPL2)
url="https://github.com/mmonaco/synaptics-led"
pkgdesc="Synaptics LED enabled psmouse kernel module"
depends=("linux>=3.2" "linux<3.3")
makedepends=("git" "linux-headers")

install=$pkgname.install

source=(
	"MD5SUMS"
	"synled.patch"
)

md5sums=(
	07f08e2927b10ae49f113aa6af06a7f7
	f882be55909191e85020ef4f8000a321
)

build() {

	msg2 "Getting source from $_gitroot"
	git archive --remote=$_gitroot v$_kver drivers/input/mouse | tar -x
	cd "drivers/input/mouse"

	msg2 "Performing Integrity Check"
	md5sum --quiet -c $srcdir/MD5SUMS

	msg2 "Patching source"
	patch -p4 -i "$srcdir/synled.patch"

	msg2 "Building psmouse.ko"
	make -C "/lib/modules/$(uname -r)/build" M="$PWD" psmouse.ko

	msg2 "Installing psmouse.ko.gz"
	gzip -9 psmouse.ko
	install -Dm 0644 psmouse.ko.gz \
		"$pkgdir/lib/modules/extramodules-$_kver-ARCH/psmouse.ko.gz"
}
