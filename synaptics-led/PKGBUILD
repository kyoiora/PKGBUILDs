# Maintainer: Matthew Monaco <dgbaley27@0x01b.net>

_kver=3.1
_gitroot=git://repo.or.cz/linux-2.6.git

pkgname=synaptics-led
pkgver=$_kver
pkgrel=2

arch=(i686 x86_64)
license=(GPL2)
url="https://github.com/mmonaco/synaptics-led"
pkgdesc="Synaptics LED enabled psmouse kernel module"
depends=("linux>=3.1" "linux<3.2")
makedepends=("git" "linux-headers")

install=$pkgname.install

source=(
	"MD5SUMS"
	"synled.patch"
)

md5sums=(
	74bd2141b9b27adcfd45310b6948825a
	f153464322a531a58aa6fd999fb6ddd0
)

build() {

	git archive --remote=$_gitroot v$_kver drivers/input/mouse | tar -x

	cd "drivers/input/mouse"

	md5sum --quiet -c $srcdir/MD5SUMS

	patch -p4 -i "$srcdir/synled.patch"

	make -C "/lib/modules/$(uname -r)/build" M="$PWD" psmouse.ko

	gzip -9 psmouse.ko

	install -Dm 0644 psmouse.ko.gz \
		"$pkgdir/lib/modules/extramodules-$_kver-ARCH/psmouse.ko.gz"
}
