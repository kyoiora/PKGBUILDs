# Maintainer: Matthew Monaco <dgbaley27@verizon.net>

_kver=3.0.1
_kname=3.0-ARCH

pkgname=synaptics-led
pkgver=$_kver
pkgrel=1

arch=(i686 x86_64)
license=(GPL2)
url="http://www.kernel.org"
pkgdesc="Synaptics LED enabled psmouse kernel module"

install=${pkgname}.install

source=(
	ftp://ftp.kernel.org/pub/linux/kernel/v3.0/linux-$_kver.tar.bz2
	synled.patch
)

md5sums=(
	aed949984b878b7fb77ffe9cbee8ce0c
	6d565328f36c6ca20b439f29bffc43bb
)

build() {

	cd "${srcdir}/linux-$_kver"
	
	rsync -r /usr/src/linux-$_kname/ .
	echo "CONFIG_MOUSE_PS2_SYNAPTICS_LED=y" >> .config
	patch -Np1 -i "${srcdir}/synled.patch"

	make ${MAKEFLAGS} drivers/input/mouse/psmouse.ko
}

package() {

	dira="${srcdir}/linux-${_kver}/drivers/input/mouse"
	dirb="${pkgdir}/lib/modules/${_kname}/updates"

	install -D -m 0644 "${dira}/psmouse.ko" "${dirb}/psmouse.ko"

	gzip -9 "${dirb}/psmouse.ko"
}
