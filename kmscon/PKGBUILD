__gitroot="git://github.com/dvdhrm/kmscon.git"
__gitname="kmscon.git"
__gitcommit="master"

pkgname=kmscon
pkgver=5+git
pkgrel=1
pkgdesc="Linux KMS/DRM based virtual console emulator"
url="https://github.com/dvdhrm/kmscon"
license=(custom:MIT)
arch=(i686 x86_64)
depends=(libgles libegl dbus-core systemd libxkbcommon pango)
makedepends=(git)
options=(!libtool)

build() {

	_git_setup
	cd "$srcdir/$pkgname-$pkgver"

	./autogen.sh --prefix=/usr --sysconfdir=/etc \
		--disable-wlterm \
		--enable-systemd \
		--enable-udev \
		--enable-hotplug \
		--disable-fbdev \
		--disable-drm \
		--enable-gles2 \
		--disable-f8x16 \
		--disable-freetype2 \
		--enable-pango
	make
}

package() {

	cd "$srcdir/$pkgname-$pkgver"
	make DESTDIR="$pkgdir" install
	install -D "docs/kmscon@.service" "$pkgdir/usr/lib/systemd/system/kmscon@.service"
	install -D "docs/kmscon.service"  "$pkgdir/usr/lib/systemd/system/kmscon.service"
	install -D "COPYING" "$pkgdir/usr/share/licenses/kmscon/custom:MIT"
}

_git_setup() {

	cd "$SRCDEST"
	
	if [[ ! -d $__gitname ]]; then
		msg2 "Cloning git repository"
		git clone --mirror "$__gitroot" "$__gitname"
	else
		msg2 "Updating git repository"
		cd "$__gitname"
		git fetch
	fi

	cd "$srcdir"
	if [[ -d $pkgname-$pkgver ]]; then
		msg2 "Removing previous build tree"
		rm -fr "$pkgname-$pkgver"
	fi

	msg2 "Creating fresh build tree"
	git clone --depth=1 -b "$__gitcommit" "file://$SRCDEST/$__gitname" "$pkgname-$pkgver"
}
