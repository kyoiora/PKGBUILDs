# Maintainer:  Matthew Monaco <net 0x01b dgbaley27>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Tobias Powalowski  <tpowa@archlinux.org>
# Contributor: Thomas BÃ¤chler <thomas@archlinux.org>
# Contributor: Alexander Baldeck <alexander@archlinux.org>

_pkgname=xf86-input-synaptics
pkgname=$_pkgname-led
pkgver=1.5.99.901
pkgrel=3
pkgdesc="Synaptics driver for notebook touchpads (with LED disable support)"
arch=('i686' 'x86_64')
license=('custom')
url="http://xorg.freedesktop.org/"
depends=('libxtst' 'mtdev')
makedepends=('xorg-server-devel>=1.11.99.902' 'libxi' 'libx11')
optdepends=('synaptics-led: patched psmouse.ko (required for LED support)')
conflicts=('xorg-server<1.11.99.902')
replaces=('synaptics')
provides=('synaptics' "${_pkgname}")
conflicts=('synaptics' "${_pkgname}")
groups=('xorg-drivers' 'xorg')
options=(!libtool)
backup=('etc/X11/xorg.conf.d/10-synaptics.conf')
source=(http://xorg.freedesktop.org/releases/individual/driver/${_pkgname}-${pkgver}.tar.bz2
        0001-Add-the-embedded-LED-support.patch
        0002-Add-tap-on-LED-feature-support.patch
        10-synaptics.conf)
md5sums=('d0c28aa15887deca4051ffea67b31d64'
         '905d891b4468b8edc0a99fcf6efa7265'
         '58a8fef68253df9dd3be5c9fad0cc84b'
         '3b81a81b958dfe3cac3cdef7ee85f1ce')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  for p in ../*.patch; do
    msg2 "Applying patch: $p"
    patch -p1 -i "$p"
  done
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/etc/X11/xorg.conf.d"
  install -m644 "${srcdir}/10-synaptics.conf" "${pkgdir}/etc/X11/xorg.conf.d/"
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"

  rm -rf "${pkgdir}/usr/share/X11"
}
