# Maintainer:  Matthew Monaco <net 0x01b dgbaley27>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Tobias Powalowski  <tpowa@archlinux.org>
# Contributor: Thomas BÃ¤chler <thomas@archlinux.org>
# Contributor: Alexander Baldeck <alexander@archlinux.org>

_pkgname=xf86-input-synaptics
pkgname=$_pkgname-led
pkgver=1.6.0
pkgrel=1
pkgdesc="Synaptics driver for notebook touchpads (with LED disable support)"
arch=('i686' 'x86_64')
license=('custom')
url="http://xorg.freedesktop.org/"
depends=('libxtst' 'mtdev')
makedepends=('xorg-server-devel>=1.11.99.902' 'libxi' 'libx11')
optdepends=('synaptics-led: patched psmouse.ko (required for LED support)')
conflicts=('xorg-server<1.11.99.902')
replaces=('synaptics')
provides=('synaptics' "${_pkgname}")
conflicts=('synaptics' "${_pkgname}")
groups=('xorg-drivers' 'xorg')
options=(!libtool)
backup=('etc/X11/xorg.conf.d/50-synaptics.conf')
source=(http://xorg.freedesktop.org/releases/individual/driver/${_pkgname}-${pkgver}.tar.bz2
        0001-Add-the-embedded-LED-support.patch
        0002-Add-tap-on-LED-feature-support.patch)
md5sums=('68a1f6b021b5ef7a7178af92ceb33dd2'
         '3198f6d39a5c1a2b2d6535e3fe738229'
         '565ff8921cf403babbbd41cb79112b8f')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  for p in ../*.patch; do
    msg2 "Applying patch: $p"
    patch -p1 -i "$p"
  done
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/etc/X11/xorg.conf.d"
  install -m644 "conf/50-synaptics.conf" "${pkgdir}/etc/X11/xorg.conf.d/"
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"

  rm -rf "${pkgdir}/usr/share/X11"
}
